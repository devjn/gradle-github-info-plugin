plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'com.jfrog.bintray' version '1.6'
    id 'net.researchgate.release' version '2.3.5'
    id 'ru.vyarus.quality' version '1.3.0'
    id 'ru.vyarus.java-lib' version '1.0.1'
    id 'ru.vyarus.github-info' version '1.0.0'
    id "com.github.ben-manes.versions" version "0.12.0"
}

sourceCompatibility = 1.6

wrapper {
    gradleVersion = 2.9
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join("\n")
    }
}

repositories { jcenter();  maven { url "https://plugins.gradle.org/m2/"} }
dependencies {
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntime files(createClasspathManifest)
    testCompile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    testCompile 'ru.vyarus:gradle-java-lib-plugin:1.0.1'
    testCompile 'com.gradle.publish:plugin-publish-plugin:0.9.2'
}

group = 'ru.vyarus'
description = 'Gradle Github info plugin'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email "vyarus@gmail.com"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY'
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['gradle', 'github']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : ''
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER'
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD'
            }
        }
    }
}

pluginBundle {
    description = 'Generates common Github links by convention and configures common plugins'
    tags = ['github']

    plugins {
        githubInfoPlugin {
            id = 'ru.vyarus.github-info'
            displayName = project.description
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload, publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events "skipped", "failed", "standard_error"
        exceptionFormat "full"
    }
    maxHeapSize = "512m"
}

dependencyUpdates {
    doFirst {
        project.group = 'foo'
    }
    revision = 'release'
}
