plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.2'
    id 'com.jfrog.bintray' version '1.4'
    id 'net.researchgate.release' version '2.3.4'
    id 'ru.vyarus.quality' version '1.0.1'
    id 'ru.vyarus.java-lib' version '1.0.1'
}

group = 'ru.vyarus'
description = 'Gradle Github info plugin'

sourceCompatibility = 1.6
targetCompatibility = 1.6

wrapper {
    gradleVersion = 2.8
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join("\n")
    }
}

repositories { jcenter();  maven { url "https://plugins.gradle.org/m2/"} }
dependencies {
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testRuntime files(createClasspathManifest)
    testCompile'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    testCompile 'ru.vyarus:gradle-java-lib-plugin:1.0.1'
    testCompile 'com.gradle.publish:plugin-publish-plugin:0.9.2'
}

pom {
    url 'https://github.com/xvik/gradle-github-info-plugin'
    licenses {
        license {
            name 'The MIT License'
            url "https://raw.githubusercontent.com/xvik/gradle-github-info-plugin/master/LICENSE"
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/xvik/gradle-github-info-plugin.git'
        connection 'scm:git@github.com:xvik/gradle-github-info-plugin.git'
        developerConnection 'scm:git@github.com:xvik/gradle-github-info-plugin.git'
    }
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email "vyarus@gmail.com"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY'
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/xvik/gradle-github-info-plugin'
        issueTrackerUrl = 'https://github.com/xvik/gradle-github-info-plugin/issues'
        vcsUrl = 'https://github.com/xvik/gradle-github-info-plugin.git'
        licenses = ['MIT']
        labels = ['gradle', 'github']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : ''
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER'
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD'
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/xvik/gradle-github-info-plugin'
    vcsUrl = 'https://github.com/xvik/gradle-github-info-plugin.git'
    description = 'Generates common Github links by convention and configures common plugins'
    tags = ['github']

    plugins {
        githubInfoPlugin {
            id = 'ru.vyarus.github-info'
            displayName = project.description
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
// HOTFIX: plugin-publish don't see properties in global gradle.properties
System.setProperty('gradle.publish.key', project.properties['gradle.publish.key'] ?: '')
System.setProperty('gradle.publish.secret', project.properties['gradle.publish.secret'] ?: '')

afterReleaseBuild.dependsOn = [bintrayUpload, publishPlugins]
afterReleaseBuild.doLast {
    logger.warn "RELEASED $project.group:$project.name:$project.version"
}

test {
    testLogging {
        events "skipped", "failed", "standardError"
        exceptionFormat "full"
    }
}
